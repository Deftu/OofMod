plugins {
    id "net.minecraftforge.gradle.forge" version "6f53277"
    id "com.github.johnrengelman.shadow" version "6.1.0"
    id "org.jetbrains.kotlin.jvm" version "1.6.10"
    id "net.kyori.blossom" version "1.3.0"
    id "java"
}

archivesBaseName = projectName
version = projectVersion
group = projectGroup

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
    makeObfSourceJar = false
}

blossom {
    replaceToken("@VERSION@", projectVersion)
    replaceToken("@NAME@", projectName)
    replaceToken("@ID@", projectId)
}

configurations {
    shade
    implementation.extendsFrom(shade)
}

repositories {
    mavenCentral()
    maven { url("https://repo.sk1er.club/repository/maven-public/") }
}

dependencies {
    shade("gg.essential:loader-launchwrapper:1.1.3")
    compileOnly("gg.essential:essential-1.8.9-forge:1788")
}

processResources {
    inputs.property("version", project.version)
    inputs.property("mcversion", project.minecraft.version)

    from(sourceSets.main.resources.srcDirs) {
        include("mcmod.info")
        expand(
                version: project.version,
                mcversion: project.minecraft.version
        )
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude("mcmod.info")
    }
}

shadowJar {
    configurations = [project.configurations.shade]
    duplicatesStrategy DuplicatesStrategy.EXCLUDE

    archiveClassifier.set("")
}

jar.manifest.attributes("TweakClass": "gg.essential.loader.stage0.EssentialSetupTweaker")
jar.finalizedBy(shadowJar)